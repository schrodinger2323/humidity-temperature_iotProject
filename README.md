# 🌡️ DHT11 ile Sıcaklık & Nem Takibi ve ThingSpeak'e Veri Gönderimi

Bu projede, DHT11 sıcaklık ve nem sensörü kullanılarak elde edilen veriler, bir I2C LCD ekran üzerinde görüntülenmekte ve aynı zamanda ESP8266 WiFi modülü ile internet üzerinden ThingSpeak sunucusuna gönderilmektedir. Gönderilen veriler, ThingSpeak arayüzü üzerinden grafiksel olarak izlenebilmektedir. 

## 🔧 Kullanılan Devre Elemanları

-Arduino UNO

-DHT11 Nem ve Sıcaklık Sensörü

-I2C LCD (16x2)

-ESP8266 WiFi Modülü

-12-F/M. 5-M/M jumper kablo

### 📡 ESP8266 Nedir? Niçin Kullanılır?
ESP8266, üzerinde WiFi modülü bulunan küçük, düşük maliyetli ve oldukça güçlü bir mikrodenetleyicidir. Genellikle nesnelerin interneti (IoT) projelerinde kullanılır. İnternete bağlanmak ve veri alışverişi yapmak için yaygın olarak tercih edilir.\
Bizim projemizde sıcaklık ve nem sensörü (DHT11) ile ölçtüğümüz verileri internet üzerinden ThingSpeak platformuna göndermek istedik. Arduino’nun kendisi internete bağlanma yeteneğine sahip olmadığı için, bu işi yapmak üzere ESP8266 modülünü kullandık.Arduino ile ESP8266 arasında yazılımsal seri iletişim kurduk ve ESP8266’ya AT komutları göndererek WiFi ağına bağlanmasını, veri iletmesini sağladık.\
ESP8266 ile haberleşmek için AT komut seti kullanılır. AT komutları sayesinde modüle bağlanma, veri gönderme, bağlantıyı kapatma gibi işlemler yaptırılabilir.\
Aşağıda bu projede kullandığımız önemli AT komutlar ve işlevleri yer almaktadır:
- **AT**	Modülün çalıştığını test eder. Eğer çalışıyorsa OK cevabı döner.
- **AT+CWMODE=1**	ESP8266’yı Client (istasyon) moduna alır. Bu sayede WiFi ağına bağlanabilir.
- **AT+CWQAP**	Daha önce bağlanılan WiFi ağından çıkış yapar.
- **AT+RST**	ESP8266’yı yeniden başlatır (reset).
- **AT+CWJAP="SSID","ŞİFRE"**	Belirtilen WiFi ağına bağlanmayı dener.
- **AT+CIPSTART="TCP","IP",80**	Belirtilen IP adresi ve port üzerinden TCP bağlantısı kurar (bizim için bu, ThingSpeak sunucusu).
- **AT+CIPSEND=n**	n bayt uzunluğunda veri göndermek üzere hazırlar.
- **GET /update?... HTTP/1.0**	ThingSpeak'e gönderilecek HTTP GET isteğidir. Sensör verilerini bu yolla sunucuya yollarız.
- **AT+CIPCLOSE**	Açık olan TCP bağlantısını kapatır.

### 🌡️ DHT11 Nedir, Neden Kullandık?
DHT11, hem sıcaklık hem de nem ölçümü yapabilen düşük maliyetli ve kullanımı kolay dijital bir sensördür. İçerisinde bir nem sensörü ve termistör (sıcaklık sensörü) barındırır. Ölçtüğü verileri dijital olarak gönderdiği için ADC (Analog-Dijital Dönüştürücü) pinlerine ihtiyaç duymaz.\
Bu projede DHT11 sensörünü kullanarak ortamın anlık sıcaklık ve nem değerlerini ölçtük ve bu verileri LCD ekranda gösterip aynı zamanda internet aracılığıyla ThingSpeak'e göndererek grafiksel olarak uzaktan izlenebilir hale getirdik. Bu sayede, çevresel koşullarını izlemek istediğimiz akıllı sistemlerin temelini oluşturmuş olduk.

### 📺 I2C Modüllü LCD Ekran Kullanmanın Avantajları
Projede kullandığımız LCD ekran, I2C modülüne sahiptir. I2C modüllü LCD sayesinde devrenin hem donanımsal hem de görsel yönetimini daha verimli şekilde gerçekleştirdik. Normalde 16x2 LCD ekranlar çalışmak için 6-8 arası pin bağlantısı isterken, I2C modülü sayesinde sadece 2 pinle (SDA ve SCL) bağlantı sağlanır. Bu da şu avantajları beraberinde getirir:
- Daha az kablo kullanımı sayesinde karmaşıklık azalır, devrenin kurulumu sade ve düzenli olur.
- Diğer pinleri boş bırakılır, böylece Arduino’nun başka görevler için daha fazla pinini kullanabiliriz.
- I2C sayesinde aynı veri yolu üzerinden birden fazla cihaz kontrol edilebilir.
- Modülün üzerinde yer alan potansiyometre, LCD ekranın parlaklığını ayarlamamıza olanak tanır. Bu sayede yazıların okunabilirliği artırılabilir. Üstelik ek potansiyometre kullanımına gerek olmadan.

### 🌐 ThingSpeak Nedir, Bu Projede Neden Kullandık?
ThingSpeak, IoT (Nesnelerin İnterneti) projeleri için geliştirilmiş ücretsiz bir veri toplama ve görselleştirme platformudur. Özellikle sensörlerden gelen verileri bulutta saklamak, grafiklerle izlemek ve başka uygulamalara aktarmak için kullanılır. MathWorks tarafından desteklendiği için MATLAB entegrasyonu da sunar.\
Biz bu projede DHT11 sensöründen aldığımız sıcaklık ve nem verilerini internete yüklemek için ThingSpeak kullandık  

## ⚙️ Bağlantılar

-DHT11-> S (signal) arduinonun 8 nolu dijital pini, + (Vcc) 5V'a, -(GND) gndye baglanir

-ESP8266-01 Wi-Fi Module 3V3, EN-> arduino 3.3V, TX->2, RX-> 3, GND->GND, diger pinleri boşta

-I2C modüllü LCD GND->GND, VCC-> 5V, SDA-> A4, SCL-> A5

## DİKKAT!
esp8266 3V toleranslıdır. RX pini arduino'nun 5V veren pinlerine direkt olarak bağlanmamalıdır. voltaj regülatörü, adaptör kullanılabilir. biz 3 tane 1K direnç kullanarak voltaj bölücü devresi yaptık. böylece esp8266nın RX pinine yaklaşık 3.3V gönderebildik. aşağıda voltaj bölücü devrenin şematik gösterimi ve bütün modüllerin breadboard üzerinde gerçeklenmiş hali bulunmaktadır: 

![WhatsApp Image 2025-04-25 at 01 41 46](https://github.com/user-attachments/assets/69e11f07-1c94-4423-b1e9-2fc2c5188db7) ![WhatsApp Image 2025-04-25 at 01 36 41](https://github.com/user-attachments/assets/d15dc9d5-2da4-4b0e-acf6-50d05aa2dcbb)



## 📡 ThingSpeak Yapılandırması ve API anahtarı alma
1. https://thingspeak.com/ adresine gidin ve hesabınız yoksa bir hesap oluşturun. Hesabınıza giriş yapın.
2. "Channels" menüsünde "New Channel" tıklanır.
3. Yeni kanalı oluşturmak için ekranda beliren formda kanalın adı "Nem&Sicaklik" girilir.
4. field1 ve field2 alanlarına "nem" ve "sıcaklık" yazılır. her iki alan için onay kutuları işaretlenir.
5. sayfanın altında Save Channel basılarak kaydedilir. 
6. API Keys sekmesine gidilerek "Write API key" altındaki API numarası kaydedilir. bu daha sonra kod içerisinde apiKey değişkeninde saklanır. 


## 📦 1. Kütüphanelerin Dahil Edilmesi
```
#include <DHT.h>               // DHT11 sıcaklık ve nem sensörü kütüphanesi
#include <LiquidCrystal_I2C.h> // I2C üzerinden LCD ekran kontrolü için kütüphane
#include <SoftwareSerial.h>    // ESP8266 ile haberleşme için yazılım seri haberleşme
```
Bu kütüphaneler sayesinde sensör okumaları yapılabilir, LCD'ye veri yazılabilir ve ESP8266 modülü ile haberleşme sağlanabilir.

DHT.h -> https://github.com/adidax/dht11

LiquidCrystal_I2C.h -> https://github.com/fdebrabander/Arduino-LiquidCrystal-I2C-library

SoftwareSerial.h-> arduino içinde hazırda var.\

yukarıda sıralanan kütüphaneler ilgili linklerden indirilip kopyalanıp arduino klasörünün altındaki library klasörüne yapıştırılması gerekir. veya arduino ide üzerinden sketch>include library> add .zip library... ile de dahil edilebilir. 



## 🌍 2. Global Değişken Tanimlamalari ve Nesneler
Bu kısımda sensörler, LCD ekran ve WiFi için gerekli tüm nesneler ve ayarlar tanımlanır:

```
#define RX 2              // ESP8266 RX pini (Arduino'dan TX çıkışı)
#define TX 3              // ESP8266 TX pini (Arduino'ya RX girişi)
#define DHTPIN 8          // DHT11 sensörünün bağlı olduğu dijital pin
#define DHTTYPE DHT11     // Kullanılan sensör tipi: DHT11

DHT dht(DHTPIN, DHTTYPE);               // DHT sensör nesnesi
LiquidCrystal_I2C lcd(0x27, 16, 2);     // LCD nesnesi (adres: 0x27, 16 sütun, 2 satır)
SoftwareSerial esp8266(RX, TX);         // ESP8266 ile yazılım seri portu oluşturuluyor

// WiFi ve ThingSpeak ayarları
String ssid = "TP-Link_4E66";                 // WiFi SSID (ağ adı)
String password = "wormhole";           // WiFi şifresi
String apiKey = "OEOBVWS4YRDPUZH9";     // ThingSpeak write API anahtarı
String host = "api.thingspeak.com";     // Sunucu adresi
String port = "80";                     // HTTP portu

// AT komutu yönetimi için sayaçlar
int countTrueCommand;
int countTimeCommand;
boolean found = false;
```
NOT:
- ssid ve password değişkenlerini kendi Wi-Fi ağınıza göre değiştirin.

- apiKey kısmına kendi ThingSpeak kanalınızın yazma API anahtarını girin.
## ⚙️ 3. setup() Fonksiyonu
Cihaz ilk açıldığında bir defaya mahsus çalışan başlatma bloğudur. LCD, DHT sensörü ve ESP8266 başlatılır, WiFi ağına bağlanılır.
```
void setup() {
  Serial.begin(9600);              // Arduino’nun seri haberleşmesi başlatılıyor
  esp8266.begin(115200);           // ESP8266 ile haberleşme başlatılıyor (baud rate 115200)

  lcd.begin();                     // LCD ekran başlatılıyor
  lcd.backlight();                 // Arka ışık açılıyor
  lcd.setCursor(0, 0);             
  lcd.print("Baglaniyor...");      // LCD'ye bilgi mesajı yazılıyor

  dht.begin();                     // DHT sensörü başlatılıyor

  // ESP8266’ya AT komutları gönderiliyor
  sendCommand("AT", 5, "OK");                                        // Modül çalışıyor mu kontrolü
  sendCommand("AT+CWMODE=1", 5, "OK");                               // İstemci modu
  sendCommand("AT+CWJAP=\"" + ssid + "\",\"" + password + "\"", 20, "OK");  // WiFi bağlantısı

  lcd.clear();                    // LCD temizleniyor
  lcd.setCursor(0, 0);
  lcd.print("Wifi Baglandi!");    // Bağlantı başarılı mesajı
  delay(2000);                    // Mesajı göstermek için 2 saniye bekle
}
```
## 🔁 4. loop() ve sendCommand() Fonksiyonları
Ana döngüde her 20 saniyede bir sıcaklık ve nem verisi alınır, LCD’ye yazılır ve ThingSpeak sunucusuna gönderilir.
```
void loop() {
  float h = dht.readHumidity();        // Nem değeri okunur
  float t = dht.readTemperature();     // Sıcaklık değeri okunur

  lcd.clear();                         // LCD ekran temizlenir
  lcd.setCursor(0, 0);                 
  lcd.print("Nem: ");
  lcd.print(h);
  lcd.print(" %");

  lcd.setCursor(0, 1);
  lcd.print("Sicaklik: ");
  lcd.print(t);
  lcd.print(" C");

  // ThingSpeak’e gönderilecek veri paketi hazırlanır
  String getData = "GET /update?api_key=" + apiKey + "&field1=" + String(h) + "&field2=" + String(t);

  sendCommand("AT+CIPMUX=1", 5, "OK");                                // Çoklu bağlantı modu
  sendCommand("AT+CIPSTART=0,\"TCP\",\"" + host + "\"," + port, 15, "OK");  // TCP bağlantısı başlat
  sendCommand("AT+CIPSEND=0," + String(getData.length() + 4), 4, ">");     // Veri gönderimine hazırla
  esp8266.println(getData);                                           // GET isteği gönderilir
  delay(2000);                                                        // Gönderim sonrası bekleme
  sendCommand("AT+CIPCLOSE=0", 5, "OK");                              // Bağlantıyı kapat

  delay(20000); // 20 saniye sonra döngü yeniden başlar
}
```
🛠 sendCommand() Fonksiyonu\
ESP8266’ya AT komutu gönderip, cevap bekleyen yardımcı fonksiyondur.
```
void sendCommand(String command, int maxTime, char readReply[]) {
  Serial.print(countTrueCommand);
  Serial.print(". AT komutu: ");
  Serial.println(command);

  while (countTimeCommand < (maxTime * 1)) {   // Belirtilen süre kadar komutu tekrar dener
    esp8266.println(command);                  // Komut gönderilir
    if (esp8266.find(readReply)) {             // Cevap aranır
      found = true;                            // Cevap bulunduysa döngüden çıkılır
      break;
    }
    countTimeCommand++;
  }

  if (found) {
    Serial.println("OK");                      // Cevap başarılıysa seri ekrana yazılır
    countTrueCommand++;
  } else {
    Serial.println("Basarisiz");               // Cevap alınamazsa hata mesajı yazılır
    countTrueCommand = 0;
  }

  countTimeCommand = 0;
  found = false;                               // Değişkenler sıfırlanır
}
```
-Gerekli kütüphaneler dahil edilip Arduino IDE üzerinden kodlar derlenir.\


## 🔧 Nasıl Çalışır?

Dört ana bölümden oluşan bu projede:
- Sensör Okuma:
DHT11 sensörü, ortam sıcaklığını santigrat (°C) ve nem oranını yüzde (%) cinsinden ölçer.
- Veri İşleme:
Arduino Uno, bu verileri dht.readTemperature() ve dht.readHumidity() fonksiyonları ile alır ve LCD ekran üzerinden kullanıcıya sunar.
- Veri Aktarımı:
Ölçülen sıcaklık ve nem verileri, yazılımsal seri port (SoftwareSerial) aracılığıyla ESP8266 modülüne iletilir. ESP8266, bu verileri ThingSpeak sunucusuna bir HTTP GET isteği olarak gönderir.
- Veri Görselleştirme:
ThingSpeak platformu, gelen bu verileri işler ve zaman serisi grafikler şeklinde kullanıcıya sunar. Bu grafikler sayesinde ortam koşulları zamana bağlı olarak takip edilebilir.
do[ru alındığı taktirde ThingSpeak verileri aşağıdaki gibi grafik şeklinde görüntüler:

![Ekran görüntüsü 2025-04-25 022538](https://github.com/user-attachments/assets/4a2e49ce-b668-48a6-a11f-8ab68aa4e50b)



















